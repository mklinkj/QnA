gretty {
	servletContainer = "tomcat10"
	contextPath = "/"
	// fileLogEnabled = false // There is no such setting in Gretty 4.1.2
	tomcat10ServletApiVersion = "${tomcat10_servlet_api_version}"
	integrationTestTask = 'integrationTest'
	jvmArgs = [
		"--add-opens=java.base/java.io=ALL-UNNAMED",
		"--add-opens=java.base/java.lang=ALL-UNNAMED",
		"--add-opens=java.rmi/sun.rmi.transport=ALL-UNNAMED"
	]
}

// In Gretty version 3.x, it was possible to run the appRun task without the /src/main/webapp directory.
// However, strangely, in version 4.x, this directory is required.
// * integrationTest Task
//   When running the integrationTest Task, it operates normally even without the webapp directory.
//   When executing the appRun Task, the webapp directory must exist.
task createWebAppDir {
	doLast {
		def webAppDir = new File(project.projectDir, '/src/main/webapp')
		if (!webAppDir.exists()) {
			webAppDir.mkdirs()
		}
	}
}

project.afterEvaluate {
	tasks.named('appRun').configure {
		dependsOn createWebAppDir
	}
}

Task prepareAppServerForIntegrationTests = project.tasks.create('prepareAppServerForIntegrationTests') {
	group = 'Verification'
	description = 'Prepares the app server for integration tests'
	doFirst {
		project.gretty {
			httpPort = -1
		}
	}
}

project.tasks.matching { it.name == "appBeforeIntegrationTest" }.all { task ->
	task.dependsOn prepareAppServerForIntegrationTests
}

project.tasks.matching { it.name == "integrationTest" }.all {
	task -> task.doFirst {
		def gretty = project.gretty
		String host = project.gretty.host ?: 'localhost'
		boolean isHttps = gretty.httpsEnabled
		Integer httpPort = integrationTest.systemProperties['gretty.httpPort']
		Integer httpsPort = integrationTest.systemProperties['gretty.httpsPort']
		int port = isHttps ? httpsPort : httpPort
		String contextPath = project.gretty.contextPath
		String httpBaseUrl = "http://${host}:${httpPort}${contextPath}"
		String httpsBaseUrl = "https://${host}:${httpsPort}${contextPath}"
		String baseUrl = isHttps ? httpsBaseUrl : httpBaseUrl
		integrationTest.systemProperty 'app.port', port
		integrationTest.systemProperty 'app.httpPort', httpPort
		integrationTest.systemProperty 'app.httpsPort', httpsPort
		integrationTest.systemProperty 'app.baseURI', baseUrl
		integrationTest.systemProperty 'app.httpBaseURI', httpBaseUrl
		integrationTest.systemProperty 'app.httpsBaseURI', httpsBaseUrl
	}
}
